plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'org.graalvm.buildtools.native' version '0.10.4'
}

group = 'com.jandi'
version = '0.0.1-SNAPSHOT'

bootJar {
    archiveFileName = 'band_backend-0.0.1-SNAPSHOT.jar'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

compileJava {
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.compilerArgs << '-parameters'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.mysql:mysql-connector-j'
    annotationProcessor 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-logging'
    
    // Logstash encoder for structured JSON logging
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'

    // Prometheus & Grafana 모니터링
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'io.micrometer:micrometer-core'
    // Prometheus exposition formats 추가 (Spring Boot 3.4.5 호환성을 위해)
    implementation 'io.prometheus:prometheus-metrics-exposition-formats'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    
    // TestContainers for integration testing
    testImplementation 'org.testcontainers:junit-jupiter:1.19.8'
    testImplementation 'org.testcontainers:mysql:1.19.8'
    testImplementation 'org.testcontainers:elasticsearch:1.19.8'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    
    // MockWebServer for external API testing
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.12.0'
    
    // AssertJ for better assertions
    testImplementation 'org.assertj:assertj-core'
    
    // H2 Database for testing
    testImplementation 'com.h2database:h2'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // AWS SDK v2 (Native Image 호환)
    implementation platform('software.amazon.awssdk:bom:2.29.29')
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:apache-client'

    // Swagger/OpenAPI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

// JaCoCo 테스트 커버리지
jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                '**/*Application*',
                '**/*Config*',
                '**/dto/**',
                '**/entity/**',
                '**/exception/**',
                '**/constant/**'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                minimum = 0.70 // 70% 커버리지 목표
            }
        }
    }
}

// 테스트 환경별 설정
test {
    systemProperty 'spring.profiles.active', 'test'
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

springBoot {
    mainClass = 'com.jandi.band_backend.JandiBandBackendApplication'
}

// GraalVM Native Image 설정
graalvmNative {
    binaries {
        main {
            imageName = 'band_backend'
            mainClass = 'com.jandi.band_backend.JandiBandBackendApplication'
            
            // 빌드 시 상세 로그 출력
            buildArgs.add('--verbose')
            buildArgs.add('-H:+ReportExceptionStackTraces')
            
            // 메모리 설정 (Native Image 빌드는 많은 메모리 필요)
            buildArgs.add('-J-Xmx8g')
            
            // 최적화 레벨
            buildArgs.add('-O3')
            
            // 리소스 포함 (application.properties 등)
            buildArgs.add('-H:IncludeResources=application.*\\.properties')
            buildArgs.add('-H:IncludeResources=logback.*\\.xml')
            buildArgs.add('-H:IncludeResources=static/.*')
            buildArgs.add('-H:IncludeResources=templates/.*')
        }
    }
}

// Native Test 설정
tasks.named('nativeTest') {
    useJUnitPlatform()
}